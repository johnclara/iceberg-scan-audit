apply from: "${project.rootDir}/gradle/customBuildscripts/scala.gradle"
apply from: "${project.rootDir}/gradle/customBuildscripts/publishing.gradle"
apply from: "${project.rootDir}/gradle/customBuildscripts/shadow.gradle"

dependencies {
    compileOnly(project(':dpiceberg-control-plane'.scala())) {
        exclude group: 'org.slf4s'
        exclude group: 'com.amazonaws'
        exclude group: 'org.json4s'
        exclude group: 'org.scala-lang'
        exclude group: 'org.scala-lang.modules'
        exclude group: 'org.apache.hadoop'
    }
    compileOnly('org.apache.iceberg:iceberg-core')
}

jar {
    enabled = false
    dependsOn(shadowJar { archiveClassifier = null })
}

shadowJar {
    configurations = [project.configurations.compileOnly]

    zip64 true

    exclude 'META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA'

    from(projectDir) {
        include 'LICENSE'
        include 'NOTICE'
    }

    // Relocate dependencies to avoid conflicts
    relocate 'com.google', 'org.apache.iceberg.shaded.com.google'
    relocate 'com.fasterxml', 'org.apache.iceberg.shaded.com.fasterxml'
    relocate 'com.github.benmanes', 'org.apache.iceberg.shaded.com.github.benmanes'
    relocate 'org.checkerframework', 'org.apache.iceberg.shaded.org.checkerframework'
    relocate 'org.apache.avro', 'org.apache.iceberg.shaded.org.apache.avro'
    relocate 'avro.shaded', 'org.apache.iceberg.shaded.org.apache.avro.shaded'
    relocate 'com.thoughtworks.paranamer', 'org.apache.iceberg.shaded.com.thoughtworks.paranamer'
    relocate 'org.apache.parquet', 'org.apache.iceberg.shaded.org.apache.parquet'
    relocate 'shaded.parquet', 'org.apache.iceberg.shaded.org.apache.parquet.shaded'
    // relocate Avro's jackson dependency to share parquet-jackson locations
    relocate 'org.codehaus.jackson', 'org.apache.iceberg.shaded.org.apache.parquet.shaded.org.codehaus.jackson'
    relocate 'org.apache.orc', 'org.apache.iceberg.shaded.org.apache.orc'
    relocate 'io.airlift', 'org.apache.iceberg.shaded.io.airlift'
}
